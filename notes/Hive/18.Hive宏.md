hive宏介绍

在编写HQL的过程中，很多逻辑需要反复使用。在关系型函数中，可以通过自定义函数来实现，Hive中实现的方法有多种。

* Hive 宏
* Hive hpl/sql
* Hive UDF

这里，我们介绍实现最简单的一种Hive 宏。语法如下:

```sql
-- 创建宏
CREATE TEMPORARY MACRO macro_name([col_name col_type, ...]) expression;
-- 删除宏
DROP TEMPORARY MACRO [IF EXISTS] macro_name;
```

创建临时宏使用给定的可选列列表作为表达式的输入创建宏。宏在当前会话期间存在。

**宏只能是临时宏，只在本次会话中可见、有效。因此你需要将宏脚本放在SQL脚本的头部。**

如果函数不存在，DROP临时宏将返回错误，除非指定了if EXISTS。

Examples：

```sql
-- 返回固定数字
CREATE TEMPORARY MACRO fixed_number() 42;
-- 字符串长度+2
CREATE TEMPORARY MACRO string_len_plus_two(x string) length(x) + 2;
-- 两数相加
CREATE TEMPORARY MACRO simple_add (x int, y int) x + y;
```

复杂案例处理：

```sql
select 
    id
    ,case degree_type when 1 then '小学'
                      when 2 then '初中'
                      when 3 then '职业高中'
                      when 4 then '中专'
                      when 5 then '高中'
                      when 6 then '大专'
                      when 7 then '本科'
                      when 8 then '硕士'
                      when 9 then '博士'
                      else null
     end as degree 
from user_info ;
```

对于case when的逻辑我们需要在多个地方使用，那么我们就可以将case when的逻辑进行封装：

```sql
-- 每次使用之前先drop，并使用if exists
drop temporary macro if exists get_degree;

create temporary macro get_degree(degree_type string)
if (degree_type is not null,
   case degree_type when 1 then '小学'
                        when 2 then '初中'
                        when 3 then '职业高中'
                        when 4 then '中专'
                        when 5 then '高中'
                        when 6 then '大专'
                        when 7 then '本科'
                        when 8 then '硕士'
                        when 9 then '博士'
                        else null
     end,null);
-- 查询
select id,get_degree(degree_type) from user_info;
```

